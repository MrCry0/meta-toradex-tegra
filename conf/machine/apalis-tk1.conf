#@TYPE: Machine
#@NAME: Toradex Apalis TK1
#@SOC: Tegra K1
#@DESCRIPTION: Machine configuration for Toradex Apalis TK1 SOM
#@MAINTAINER: Marcel Ziswiler <marcel.ziswiler@toradex.com>

MACHINE_NAME = "Apalis-TK1"

require conf/machine/include/soc-family.inc

SOC_FAMILY = "tegra124"

PREFERRED_PROVIDER_virtual/kernel = "linux-toradex"

KERNEL_IMAGETYPE = "zImage"
KERNEL_DEVICETREE += "tegra124-apalis-eval.dtb tegra124-apalis-v1.2-eval.dtb"

MACHINE_BOOT_FILES = "boot.scr"

# wic support
IMAGE_BOOT_FILES ?= " \
    ${KERNEL_IMAGETYPE} \
    ${KERNEL_DEVICETREE} \
    ${MACHINE_BOOT_FILES} \
"
WKS_FILE_DEPENDS ?= " \
    virtual/bootloader \
    u-boot-distro-boot \
    \
    e2fsprogs-native \
    bmap-tools-native \
"

WKS_FILE = "sdimage-bootpart-tegra.wks"
WIC_CREATE_EXTRA_ARGS_append = " --no-fstab-update"

# The kernel lives in a seperate FAT or UBI partition, don't deploy it in /boot
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

PREFERRED_PROVIDER_u-boot = "u-boot-toradex-tk1"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-toradex-tk1"
PREFERRED_PROVIDER_u-boot-fw-utils ?= "u-boot-toradex-fw-utils-tk1"
UBOOT_MACHINE = "apalis-tk1_defconfig"
UBOOT_ENTRYPOINT = "0x80008000"
UBOOT_LOADADDRESS = "${UBOOT_ENTRYPOINT}"
# The payload offsets are given in number of 512 byte blocks.
OFFSET_BOOTROM_PAYLOAD = "0"

TEZI_DISTRO_BOOT_SCRIPTS = "boot.scr"
TEZI_UBOOT_BINARY_EMMC = "apalis-tk1.img"

# require u-boot-dtb-tegra.bin to be used
UBOOT_IMAGE = "u-boot-dtb-tegra-${MACHINE}-${PV}-${PR}.${UBOOT_SUFFIX}"
UBOOT_BINARY = "u-boot-dtb-tegra.${UBOOT_SUFFIX}"
UBOOT_SYMLINK = "u-boot-dtb-tegra-${MACHINE}.${UBOOT_SUFFIX}"

PREFERRED_PROVIDER_lxde-icon-theme_apalis-tk1 = "lxde-icon-theme"
PREFERRED_PROVIDER_lxsession-lite_apalis-tk1 = "lxsession"

PREFERRED_PROVIDER_virtual/xserver ?= "xserver-xorg"
PREFERRED_PROVIDER_virtual/mesa ?= "mesa"
PREFERRED_PROVIDER_virtual/egl ?= "linux-driver-package"
PREFERRED_PROVIDER_virtual/libgl ?= "linux-driver-package"
PREFERRED_PROVIDER_virtual/libgles1 ?= "linux-driver-package"
PREFERRED_PROVIDER_virtual/libgles2 ?= "linux-driver-package"

#xf86-video-nouveau
#our fs-init may conflict with linux-driver-package-firstboot
XSERVER ?= " \
    xserver-xorg \
    mesa-driver-swrast \
    xf86-input-evdev \
    xf86-input-keyboard \
    xf86-input-mouse \
    xf86-video-modesetting \
    linux-driver-package \
"

MACHINE_FEATURES = "screen usbgadget usbhost vfat ext2 alsa touchscreen wifi bluetooth 3g pci "
MACHINE_EXTRA_RDEPENDS = " \
    tdx-k20-fw \
    linux-driver-package-boot \
    linux-driver-package-firmware \
    linux-driver-package-firstboot \
"
EXTRA_IMAGEDEPENDS += "virtual/bootloader"

# used by sysvinit_2
SERIAL_CONSOLES = "115200;ttyS0"
IMAGE_CLASSES += "image_type_tezi"
IMAGE_FSTYPES += "wic.bmap wic.gz teziimg"

TORADEX_PRODUCT_IDS = "0034"
TORADEX_FLASH_TYPE = "emmc"

DEFAULTTUNE = "cortexa15hf-neon"
require conf/machine/include/tune-cortexa15.inc
